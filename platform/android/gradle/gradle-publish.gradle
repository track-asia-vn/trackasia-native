apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: "${rootDir}/gradle/artifact-settings.gradle"
apply from: "${rootDir}/gradle/publish-root.gradle"

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath = files(android.bootClasspath)
    failOnError = false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.sourceFiles
}

tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('docencoding', 'UTF-8')
    options.addStringOption('charset', 'UTF-8')
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}


project.logger.lifecycle(ext["versionName"])

version = project.ext.versionName
group = project.ext.trackasiaArtifactGroupId

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId this.group
                artifactId project.ext.trackasiaArtifactId
                version this.version
                
                from components.release

                artifact androidSourcesJar
                artifact androidJavadocsJar                

                pom {
                    name = project.ext.trackasiaArtifactTitle
                    description = project.ext.trackasiaArtifactTitle
                    url = project.ext.trackasiaArtifactUrl
                    licenses {
                        license {
                            name = project.ext.trackasiaArtifactLicenseName
                            url = project.ext.trackasiaArtifactLicenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = project.ext.trackasiaDeveloperId
                            name = project.ext.trackasiaDeveloperName
                            email = 'trackasia@trackasia.org'
                        }
                    }
                    scm {
                        connection = project.ext.trackasiaArtifactScmUrl
                        developerConnection = project.ext.trackasiaArtifactScmUrl
                        url = project.ext.trackasiaArtifactUrl
                    }
                }
            }
        }
    }
}

afterEvaluate { project ->
    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompile.classpath.files)
        }
    }
}

signing {
    sign publishing.publications
}

